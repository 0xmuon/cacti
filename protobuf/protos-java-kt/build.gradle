buildscript {
    ext.coroutines_version = "1.3.7"
    ext.protobuf_version = "3.11.1"
    ext.grpc_version = "1.29.0" // CURRENT_GRPC_VERSION
    ext.grpc_kotlin_version = "0.1.4" // CURRENT_GRPC_KOTLIN_VERSION
	ext.release_version = "1.2.0"
}

plugins {
    id "maven-publish"
	id 'java'
	id 'com.google.protobuf' version '0.8.12'
}

Properties constants = new Properties()
file("$projectDir/./artifactory.properties").withInputStream { constants.load(it) }

repositories {
	mavenLocal()
	jcenter()
	mavenCentral()
	maven { url "https://dl.bintray.com/arrow-kt/arrow-kt/" }
	maven { url 'https://software.r3.com/artifactory/corda' }
	maven { url 'https://jitpack.io' }
	maven {
		url 'https://maven.pkg.github.com/hyperledger-labs/weaver-dlt-interoperability'
		credentials {
			username constants.username
			password constants.password
		}
	}
}

dependencies {
	compile "com.google.protobuf:protobuf-java:$protobuf_version"
	implementation "com.google.protobuf:protobuf-java-util:$protobuf_version"
	implementation "io.grpc:grpc-netty-shaded:$grpc_version"
    implementation "io.grpc:grpc-protobuf:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"
	implementation "io.grpc:grpc-kotlin-stub:$grpc_kotlin_version"

}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobuf_version"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
        }
		grpckt {
			artifact = "io.grpc:protoc-gen-grpc-kotlin:$grpc_kotlin_version"
		}
    }
	generateProtoTasks {
        all().each { task ->
            task.plugins {
                // Generate Java gRPC classes
                grpc { }
                // Generate Kotlin gRPC using the custom plugin from library
                grpckt { }
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src"
}

sourceSets {
  main {
    proto {
      // In addition to the default "src/main/proto"
	  srcDir "./fabric-protos"
	  srcDir "../../common/interop-protos"
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

publishing {
    publications {
        protos(MavenPublication) {
			groupId = 'com.weaver'
			artifactId = 'protos-java-kt'
            version = "${release_version}"
            from components.java
            artifact (sourcesJar) {
                classifier = "sources"
            }
        }
    }
	repositories {
		maven {
			url 'https://maven.pkg.github.com/hyperledger-labs/weaver-dlt-interoperability'
			credentials {
				username constants.username
				password constants.password
			}
		}
	}
}